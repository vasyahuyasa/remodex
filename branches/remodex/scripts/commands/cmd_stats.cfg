// show player stats, -1 all

//returns string containing player's statistics
getplayerstatstr = [
	str = (format "^f1Name: ^f7%1^f1 Frags: ^f7%2^f1 Deathes: ^f7%3^f1 Accuracy: ^f7%4%%^f1 Kpd: ^f7%5^f1%" (getname $arg1) (getfrags $arg1) (getdeaths $arg1) (getaccuracy $arg1) (getkpd $arg1))	
	curr_mode = (modetostr (getmode))
	
	//current gamemode is team mode
	if (= (isinlist $TEAMMODES $curr_mode) 1) [
		str = (concatword $str (format " Teamkills: ^f7%1^f1" (getteamkills $arg1)))
		
		//current gamemode has flags
		if (= (isinlist $FLAGMODES $curr_mode) 1) [
			str = (concatword $str (format " Scored flags: ^f7%1^f1" (getflags $arg1) ))
			//str = (concatword $str (format " Returned flags: %1 " (getretflags $arg1)))  not implemented yet
		]
	]
	str = (concatword $str (format " Rank: ^f7%1^f1" (getrank $arg1))) 
	str = (concatword $str (format " Time: ^f7%1^f1" (formatmillis "%hh %mm %ss" (getonline $arg1))))
	result $str
]

cmd_stats = [
	//show stat for current player 
	if (=s $arg2 "") [
		pm $arg1 (getplayerstatstr $arg1)
	] [
		//show statistics for all
		if (strcmp $arg2 "-1") [
			rank = ""
			all = (sortlist (allactiveplayers))
			looplist cn $all [	
				rank = (concat $rank (getrank $cn))
			]

			all = (sorttwolists $rank $all) 
			looplist cn $all [	
				str = (getplayerstatstr $cn)
				if (= $cn $arg1) [str = (concat $str "^f0*")]
				pm $arg1 $str
			]
		] [
			pm $arg1 (getplayerstatstr $arg2)
		]
	]
]

registercommand "stats" cmd_stats 1 "|p" "stats [cn] ^f1Shows game statistics for player cn or for all players if passed -1"
