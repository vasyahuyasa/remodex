//Spy module. Logs ip addresses and names of players.

spy_table = "spy"

defaultvalue "spy_db" "default"

//Log player's ip
spy_logname = [ 
	db_init $spy_db
	name = (getvalue (getalias "arg2") (getname $arg1))
	ip = (ip2int (getip $arg1))
	last_seen = (systimef "%s")
	
	err = (db_insert_replace $spy_table "VALUES (:0, ':1', :2)" (concat $ip $name $last_seen) $spy_db) 
	if (=s $err -1) [
		db_error $spy_db
	]
]
addhandler onconnect spy_logname
addhandler onswitchname spy_logname


//returns player's last connection time and ip. or "" if not found
spy_lastseen = [
	db_init $spy_db
	
	query = (format "SELECT ip, lastseen FROM %1 WHERE name=':0' ORDER BY lastseen DESC" $spy_table)
	qh = (db_pquery $query $arg1 $spy_db)
	res = ""
	// check for errors
	if (= $qh -1) [
		db_error $spy_db
		res = "^f5Database error"
	] [
		res = (db_getrow $qh $spy_db)
		(db_finalize $qh $spy_db)
		
		if (=s $res "") [
			res = (format "^f1Player ^f0%1 ^f1have never connected" $arg1)	
		] [
			res = (format "^f1Player ^f0%1 ^f1was connected from ip ^f0%2 ^f1at ^f0%3" $arg1 (int2ip (at $res 0)) (timef (at $res 1) "%d.%m.%Y, %X"))
		]
		
	]
	result $res
]

cmd_lastseen = [
	pm $arg1 (spy_lastseen $arg2)
]
registercommand "lastseen" cmd_lastseen 3 "w" "lastseen [nickname] ^f1Show last time player was connected to the server and player's ip" 

irccmd_lastseen = [
	ircsay (spy_lastseen $arg2)
]
irc_registercommand "lastseen" irccmd_lastseen 2 "w" "lastseen [nickname]. Show last time player was connected to the server and player's ip"


//return all names (limited to 100) of player
spy_whois = [
	ip = (ip2int (getip $arg1))	
	db_init $spy_db
	if (! $arg2) [
		qh = (db_pquery (format "SELECT name FROM %1 WHERE ip=:0 ORDER BY lastseen DESC" $spy_table) $ip $spy_db)
	] [
		if (= $arg2 1) [
			ip = (& $ip 0xFFFFFF00)
			ip2 = (+ $ip 0xFF)
		] [
			ip = (& $ip 0xFFFF0000)
			ip2 = (+ $ip 0xFFFF)
		]
		qh = (db_pquery (format "SELECT name FROM %1 WHERE ip>=:0 AND ip<=:1 ORDER BY lastseen DESC" $spy_table) (concat $ip $ip2) $spy_db)
	] 
	
	res = ""
	while [row = (db_getrow $qh $spy_db); result (!=s $row "")] [
		res = (concat $res $row)
	]
	
	db_finalize $qh $spy_db
	result $res
]

cmd_whois = [
	pm $arg1 (format "^f1All names for ^f0%1^f1: ^f0%2" (getname $arg2) (spy_whois $arg2 (getvalue (getalias "arg3") 0)))
]
registercommand "whois" cmd_whois 3 "c|i" "whois [cn] (0|1|2) ^f1Search in database all nicknames with player's ip (if param = 0). If param = 1 search also in the entire subnetwork (e.g. 192.168.1.*). If 2 search in 192.168.*.*"


irc_whois = [
	ircsay (format "^f1All names for ^f0%1^f1: ^f0%2" (getname $arg2) (spy_whois $arg2 (getvalue (getalias "arg3") 0)))
]
irc_registercommand "whois" irc_whois 2 "c|i" "whois [cn] (0|1|2). Search in database all nicknames with player's ip (if param = 0). If param = 1 search also in the entire subnetwork (e.g. 192.168.1.*). If 2 search in 192.168.*.*"
