flagkeeper_good_starttime = -1
flagkeeper_evil_starttime = -1

defaultvalue "flagrun_show_messages" 1
defaultvalue "flagrun_minmillis" 3000 //3 seconds
defaultvalue "flagrun_db" 0

// best score time
// flugrun_best_5_reissen = "9602 |RB|degrave"

// flagrun_addresult $run(ms) $name(str)
flagrun_addresult = [
	flagrun_modestring = (concatword "flagrun_best_" (getmode) "_" (getmap))
	flagrun_modescore = (getalias $flagrun_modestring)

	// check for not defined time and new record
	if (|| (=s $flagrun_modescore "") (< $arg1 (at $flagrun_modescore 0))) [
		alias $flagrun_modestring (concat $arg1 $arg2)
		if $flagrun_db [ flagrun_db_addresult $arg1 $arg2 ]
	]
]

flagrun_bestresult = [
	flagrun_modestring = (concatword "flagrun_best_" (getmode) "_" (getmap))
	result (getalias $flagrun_modestring)
]

flagrun_ontakeflag = [
	//if flag was taken from base
	if (= $arg3 1) [
		if (=s $arg2 "evil") [
			flagkeeper_good_starttime = $totalmillis
		] [
			flagkeeper_evil_starttime = $totalmillis
		]
	]
]
	
flagrun_ondropflag = [
	if (=s $arg2 "evil") [
		flagkeeper_good_starttime = 0
	] [
		flagkeeper_evil_starttime = 0
	]
]

flagrun_onscoreflag = [
	flagkeeperstr = (concatword "flagkeeper_" $arg2 "_starttime")
	flagkeeper_starttime = (getalias $flagkeeperstr)
	// if flag taken from base
	if (> $flagkeeper_starttime 0) [
		flagrun_millis = (abs (- $totalmillis $flagkeeper_starttime))

		// check for teleport hack
		if (>= $flagrun_millis $flagrun_minmillis) [
			// teleport hack not detected, add result to table and check for new record
			flagrun_addresult $flagrun_millis (getname $arg1)
			flagrun_best = (flagrun_bestresult)
			flagrun_msg = (format "^f0%1 ^f7scored flag in ^f0%2 sec" (getname $arg1) (divf $flagrun_millis 1000))
		
			// check for new record
			if (=s $flagrun_best (concat $flagrun_millis (getname $arg1))) [
				flagrun_msg = (format "%1 ^f7(New record)" $flagrun_msg)
			] [
				flagrun_msg = (format "%1 ^f7(best: ^f0%2 %3 sec^f7)" $flagrun_msg (at $flagrun_best 1) (divf (at $flagrun_best 0) 1000) )
			]
		
			if $flagrun_show_messages [ say $flagrun_msg ]
		] [
			// detected teleport hack or speedhack
			// show some warnings
			say (format "^f3Warning! %1 may use the teleport hack" (getname $arg1))
			ircsay (format "^f3Warning! %1(%2) may use the teleport hack" (getname $arg1) $arg1)
		]

		// kick imidetly if flagrun time below 300 ms
		if (<= $flagrun_millis 300) [ kick $arg1 ]
		
		alias $flagkeeperstr 0
	]
]

flagrun_onreturnflag = [
	if (=s $arg2 "evil") [
		flagkeeper_good_dropped = 0
	] [
		flagkeeper_evil_dropped = 0
	]
]

flagrun_onresetflag = [
	if (=s $arg2 "evil") [
		flagkeeper_good_dropped = 0
	] [
		flagkeeper_evil_dropped = 0
	]
]

flagrun_onmapchange = [
	flagkeeper_good_dropped = 0
	flagkeeper_evil_dropped = 0
]



// database staff 
// int mode (uniq) | varchar[20] map (uniq) | varchar[20] name | int time

flagrun_table = "flagrun" // hardcoded table name

//load results from database
flagrun_db_loadtime = [
	db_init $flagrun_db
	echo (format "Loading flagrun results from database '%1'" $flagrun_db)
	// make query
	frun_query = (format "SELECT mode, map, name, time FROM %1" $flagrun_table)
	qh = (db_query $frun_query $flagrun_db)
	// check for errors
	if (= $qh -1) [
		db_error $flagrun_db
	] [
		// no errors
		// create aliases for records		
		while [row = (db_getrow $qh $flagrun_db); result (!=s $row "")] [
			// row format "17 wdcd |RB|degrave 3423"
			// create aliases
			
			frun_aname = (concatword "flagrun_best_" (at $row 0) "_" (at $row 1))
			frun_avalue = (concat (at $row 3) (at $row 2))
			
			echo $frun_aname
			alias $frun_aname $frun_avalue
		]
		db_finalize $qh $flagrun_db
	]
]

// add result to database
flagrun_db_addresult = [
	// create table flagrun(mode UNSIGNED TINYINT, map VARCHAR(20), name VARCHAR(20), time UNSIGNED INTEGER, unique(mode, map));

	// variables for insert
	frun_mode = (getmode)
	frun_map = (getmap)
	frun_name = $arg2
	frun_time = $arg1	

	// make query
	
	//db_init
	//db_query "BEGIN TRANSACTION"
	err = (db_insert_replace $flagrun_table "VALUES (:0, ':1', ':2', :3)" (concat $frun_mode $frun_map $frun_name $frun_time) $flagrun_db)
	//db_query "COMMIT"
	
	// check for errors
	if (=s $err -1) [
		db_error $flagrun_db
	]
	echo (format "Added new result to flagrun database %1 %2" $frun_name $frun_time)
]

// load all records from database
if $flagrun_db [ flagrun_db_loadtime ]

addhandler ontakeflag flagrun_ontakeflag
addhandler ondropflag flagrun_ondropflag
addhandler onscoreflag flagrun_onscoreflag

addhandler onreturnflag flagrun_onreturnflag
addhandler onresetflag flagrun_onresetflag

addhandler onmapchange flagrun_onmapchange
