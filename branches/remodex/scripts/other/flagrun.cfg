flagrun_show_messages = 1

flagkeeper_good_starttime = -1
flagkeeper_evil_starttime = -1

// best score time
// flugrun_best_5_reissen = "9602 |RB|degrave"

// flagrun_addresult $run(ms) $name(str)
flagrun_addresult = [
	flagrun_modestring = (concatword "flugrun_best_" (getmode) "_" (getmap))
	flagrun_modescore = (getalias $flagrun_modestring)
	// run time not defined
	if (=s $flagrun_modescore "") [
		alias $flagrun_modestring (concat $arg1 $arg2)
	] [
		// if new run time better than prev
		if (< $arg1 (at $flagrun_modescore 0)) [
				alias $flagrun_modestring (concat $arg1 $arg2) 
		]
	]
]

flagrun_bestresult = [
	flagrun_modestring = (concatword "flugrun_best_" (getmode) "_" (getmap))
	result (getalias $flagrun_modestring)
]

srv_ontakeflag = [
	//if flag was taken from base
	if (= $arg3 1) [
		if (=s $arg2 "evil") [
			flagkeeper_good_starttime = $totalmillis
		] [
			flagkeeper_evil_starttime = $totalmillis
		]
	]
]
	
srv_ondropflag = [
	if (=s $arg2 "evil") [
		flagkeeper_good_starttime = 0
	] [
		flagkeeper_evil_starttime = 0
	]
]

srv_onscoreflag = [
	flagkeeperstr = (concatword "flagkeeper_" $arg2 "_starttime")
	flagkeeper_starttime = (getalias $flagkeeperstr)
	// if flag taken from base
	if (> $flagkeeper_starttime 0) [
		flagrun_millis = (- $totalmillis $flagkeeper_starttime)
		// add result to table and check for new record
		flagrun_addresult $flagrun_millis (getname $arg1)
		flagrun_best = (flagrun_bestresult)
		flagrun_msg = (format "^f0%1 ^f7scored flag in ^f0%2 sec" (getname $arg1) (divf $flagrun_millis 1000))
		
		// check for new record
		if (=s $flagrun_best (concat $flagrun_millis (getname $arg1))) [
			flagrun_msg = (format "%1 ^f7(New record)" $flagrun_msg)
		] [
			flagrun_msg = (format "%1 ^f7(best: ^f0%2 %3 sec^f7)" $flagrun_msg (at $flagrun_best 1) (divf (at $flagrun_best 0) 1000) )
		]
		
		if $flagrun_show_messages [ say $flagrun_msg ]
		alias $flagkeeperstr 0
	]
]

srv_onreturnflag = [
	if (=s $arg2 "evil") [
		flagkeeper_good_dropped = 0
	] [
		flagkeeper_evil_dropped = 0
	]

]

srv_onresetflag = [
	if (=s $arg2 "evil") [
		flagkeeper_good_dropped = 0
	] [
		flagkeeper_evil_dropped = 0
	]
]

srv_flag_onmapchange = [
	flagkeeper_good_dropped = 0
	flagkeeper_evil_dropped = 0
]

addhandler ontakeflag srv_ontakeflag
addhandler ondropflag srv_ondropflag
addhandler onscoreflag srv_onscoreflag

addhandler onreturnflag srv_onreturnflag
addhandler onresetflag srv_onresetflag

addhandler onmapchange srv_flag_onmapchange

