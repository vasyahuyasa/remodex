// Spy module. Logs ip addresses and names of players.
// Requires database connection (MySQL is recommended, but SQLite could be used with some performance issues), creates table automatically unless it exists
// Table schema: see $spytable_sqlite and $spytable_mysql


defaultvalue "spy_table" "spy"
defaultvalue "spy_db" "default"


not_recommended_with_sqlite "spy" $spy_db

sqlite_create_table = "CREATE TABLE `:0` (ip INTEGER, name VARCHAR(40), lastseen UNSIGNED INTEGER, cnt UNSIGNED INTEGER default 0, unique(ip, name))"
mysql_create_table = "CREATE TABLE IF NOT EXISTS :0 (ip int(4) NOT NULL, name varchar(40) NOT NULL, lastseen int(10) unsigned NOT NULL, cnt int(10) unsigned NOT NULL DEFAULT '1', PRIMARY KEY (ip,name), KEY name (name), KEY ip (ip)) ENGINE=InnoDB DEFAULT CHARSET=utf8"
db_check_table "spy" $spy_db $spy_table "ip name lastseen cnt" $sqlite_create_table $mysql_create_table

//************************************************


//Log player's ip
spy_logname = [ 
	db_init $spy_db
	name = (getvalue "arg2" (getname $arg1)) // if user renamed, $arg2 is not empty
	ip = (ip2int (getip $arg1))
	lastseen = (systimef "%s")
	
	db__uid = (db_get_dbuid $spy_db)
	
	cases (db_get_engine $spy_db) "sqlite3" [
		sqlite3_query $db__uid "BEGIN TRANSACTION"
		sqlite3_pquery $db__uid "INSERT OR IGNORE INTO `:0` (ip, name) VALUES (:1, ':2')" $spy_table $ip $name
		sqlite3_pquery $db__uid "UPDATE `:0` SET lastseen=:3, cnt=cnt+1 WHERE ip=:1 AND name LIKE ':2'" $spy_table $ip $name $lastseen
		res = (sqlite3_query $db__uid "COMMIT")
	] "mysql" [ 
		res = (mysql_pquery $db__uid "INSERT INTO `:0` (ip, name, lastseen) VALUES (':1', ':2', ':3') ON DUPLICATE KEY UPDATE lastseen=VALUES(lastseen), cnt=cnt+1" $spy_table $ip $name $lastseen)
	] () [
		res = -1
	]
	if (= $res -1) [
		db_error $spy_db
	]
]

addhandler onconnect spy_logname
addhandler onswitchname spy_logname


//returns player's last connection time and ip. or "" if not found
spy_lastseen = [
	db_init $spy_db
	
	query = "SELECT ip, lastseen FROM :0 WHERE name LIKE ':1' ORDER BY lastseen DESC LIMIT 0,1"
	
	
	db__uid = (db_get_dbuid $spy_db)
	cases (db_get_engine $spy_db) "sqlite3" [
		qh = (sqlite3_pquery $db__uid $query $spy_table $arg1)
	] "mysql" [ 
		qh = (mysql_pquery $db__uid $query $spy_table $arg1)
	] () [
		qh = -1
	]
	
	res = ""
	// check for errors
	if (= $qh -1) [
		db_error $spy_db
		res = "^f5Database error"
	] [
		res = (db_getrow $qh $spy_db)
		db_finalize $qh $spy_db
		if (=s $res "") [
			res = (format "^f1Player ^f0%1 ^f1has never connected" $arg1)	
		] [
			res = (format "^f1Player ^f0%1 ^f1was connected from ip ^f0%2 ^f1at ^f0%3" $arg1 (int2ip (at $res 0)) (timef (at $res 1) "%d.%m.%Y, %X"))
		]
		
	]
	
	result $res
]


//return all names (limited to 100) of player
spy_whois = [
	ip = (ip2int (getip $arg1))	
	db_init $spy_db
	if (! $arg2) [
		qh = (db_query (format "SELECT name FROM %1 WHERE ip=%2 ORDER BY cnt DESC LIMIT 0,5" $spy_table $ip) $spy_db)
	] [
		if (=s $arg2 "1") [
			ip = (& $ip 0xFFFFFF00)
			ip2 = (+ $ip 0xFF)
		] [
			ip = (& $ip 0xFFFF0000)
			ip2 = (+ $ip 0xFFFF)
		]
		qh = (db_query (format "SELECT name FROM %1 WHERE ip>=%2 AND ip<=%3 ORDER BY cnt DESC LIMIT 0,5" $spy_table concat $ip $ip2) $spy_db)
	] 
	
	res = ""
	while [row = (db_getrow $qh $spy_db); result (!=s $row "")] [
		res = (concat $res (listat $row 0)))
	]
	
	db_finalize $qh $spy_db
	result $res
]
