// Some general database stuff

//informal rule: all "local" variables should start with "db__" 

//register database in cubescript
// db_register  $engine $conenction_string $name
// engine - mysql, sqlite3
// connection string  (login:password@server:port/database_name  for mysql  or path/to/file.db for sqlite)
// name - name of database ("default" for default database)
// db_register  "sqlite" "base.db" "default"
db_register = [
	if (|| (&& (=s $arg1 "sqlite3") (identexists "sqlite3_open")) (&& (=s $arg1 "mysql") (identexists "mysql_open"))) [
		//checking available of egine specific functions.
		db__name = (getvalue (getalias "arg3") "default")
		
		alias (concatword "db__engine_" $db__name) $arg1
		alias (concatword "db__str_" $db__name) $arg2
		alias (concatword "db__dbuid_" $db__name "_0") "-1"
		
		echo (format "Registered database '%1' %2://%3" $db__name $arg1 $arg2)
	] [
		echo (format "DB ERROR: Database engine %1 was not compiled in server" $arg1)
	]
]

//get registered database engine
// db_get_engine $name
// db_get_engine "default"
db_get_engine = [
	result (getalias (concatword "db__engine_" (getvalue (getalias "arg1") "default")))
]

//get registered database connection string
// db_get_connstr $name
// db_get_connstr "default"
db_get_connstr = [
	result (getalias (concatword "db__str_" (getvalue (getalias "arg1") "default")))
]


//get connection dbuid
// db_get_dbuid $name $conn_num
// db_get_dbuid "default" 0
db_get_dbuid = [
	result (getalias (concatword "db__dbuid_" (getvalue (getalias "arg1") "default") "_" (getvalue (getalias "arg2") "0")))
]

//checks if connection dbuid with specified conn_num is correct. if no - prints error message
// db_check_dbuid $name $conn_num
// db_check_dbuid "default" 0
db_check_dbuid = [
	db__name = (getvalue (getalias "arg1") "default")
	db__conn_num = (getvalue (getalias "arg2") "0")
	db__uid = (getalias (concatword "db__dbuid_" $db__name "_" $db__conn_num))
	if (= $db__uid -1) [
		db_print_error $db__name "could not initialize connection"
		db__res = 0
	] [
		db__res = 1
	]
	result $db__res
]

//set dbuid
// db_set_dbuid $dbuid $name $conn_num 
// db_set_dbuid $dbuid "default" 0 
db_set_dbuid = [
	alias (concatword "db__dbuid_" (getvalue (getalias "arg2") "default") "_" (getvalue (getalias "arg3") "0")) $arg1
]

//print error
// db_print_error $message $name
// db_print_error "database was stolen by Cthulhu" "default"
db_print_error = [
	db__name = (getvalue (getalias "arg2") "default")
	echo  (format "DB ERROR in %2 database '%1': %3" $db__name (db_get_engine $db__name) $arg1))
]

// get database error message and print it
// db_error $name $conn_num 
// db_error "default" 0 
db_error = [
	db__name = (getvalue (getalias "arg1") "default")
	db__conn_num = (getvalue (getalias "arg2") "0")
	db__uid = (db_get_dbuid $db__name $db__conn_num)
	
	db__error = 0
	cases (db_get_engine $db__name) "sqlite3" [
		db__error = (sqlite3_error $db__uid)
	] "mysql" [
		db__error = (mysql_error $db__uid)
	] () [
	]
	if $db__error [
		db_print_error $db__error $db__name
	]
]

// open connection to db
// db_open $name $conn_num
// db_open "default" 0
db_open = [
	db__name = (getvalue (getalias "arg1") "default")
	db__conn_num = (getvalue (getalias "arg2") "0")
	db__connection_str = (db_get_connstr $db__name)
	
	cases (db_get_engine $db__name) "sqlite3" [
		db__uid = (sqlite3_open $db__connection_str)
	] "mysql" [ 
		db__uid = (mysql_open $db__connection_str)
	] () [
		db__uid = -1
		echo (concatword "DB ERROR: unknown db engine " $db__name)
	]
	result $db__uid
]



// initialize database connection  
// if connection is not already open - open it
// db_init $name $conn_num
// db_init "default" 0
db_init = [
	db__name = (getvalue (getalias "arg1") "default")
	db__conn_num = (getvalue (getalias "arg2") "0")
	db__connection_str = (db_get_connstr $db__name)
	db__uid = (db_get_dbuid $db__name $db__conn_num)
	
	if (= $db__uid -1) [
		cases (db_get_engine $db__name) "sqlite3" [
			db_set_dbuid (sqlite3_open $db__connection_str) $db__name $db__conn_num
		] "mysql" [ 
			db_set_dbuid (mysql_open $db__connection_str) $db__name $db__conn_num
		] () [ 
			echo (concatword "db error: unknown db engine " $db__name)
		]
		if (db_check_dbuid $arg1 $arg2) [
			echo (format "Connected to database '%1' %2://%3" $db__name (db_get_engine $db__name) $db__connection_str)
		]
	]
]

// execute sql query
// db_query $query $name $conn_num
// db_query "select * from table where id=1 and name='123'" "default"  0
db_query = [
	db__name = (getvalue (getalias "arg2") "default")
	db__conn_num = (getvalue (getalias "arg3") "0")
	db__uid = (db_get_dbuid $db__name $db__conn_num)
	cases (db_get_engine $db__name) "sqlite3" [
		db__res = (sqlite3_query $db__uid $arg1)
	] "mysql" [
		db__res = (mysql_query $db__uid $arg1)
	] () [
		db__res = -1
	]
	result $db__res
]


// execute sql query with filtering parameters
// db_pquery $query $parameters $name $conn_num
// db_query "select * from table where id=:0 and name=':1'" "1123 supername" "default"  0
db_pquery = [
	db__name = (getvalue (getalias "arg3") "default")
	db__conn_num = (getvalue (getalias "arg4") "0")
	db__uid = (db_get_dbuid $db__name $db__conn_num)
	cases (db_get_engine $db__name) "sqlite3" [
		db__res = (sqlite3_pquery $db__uid $arg1 $arg2)
	] "mysql" [
		db__res = (mysql_pquery $db__uid $arg1 $arg2)
	] () [
		db__res = -1
	]
	result $db__res
]

// insert or replace 
// db_insert_replace $tablename $last_query $parameters $name $conn_num
// db_insert_replace "player" "(name) VALUES (':0')" "|RB|^o_o^" $name $conn_num
db_insert_replace = [
	db__name = (getvalue (getalias "arg4") "default")
	db__conn_num = (getvalue (getalias "arg5") "0")
	db__uid = (db_get_dbuid $db__name $db__conn_num)
	cases (db_get_engine $db__name) "sqlite3" [
		db__res = (sqlite3_pquery $db__uid (concatword "INSERT OR REPLACE INTO " $arg1 " " $arg2) $arg3)
	] "mysql" [
		db__res = (mysql_pquery $db__uid (concatword "REPLACE INTO " $arg1 " " $arg2) $arg3)
	] () [
		db__res = -1
	]
	result $db__res
]


// get row
// db_getrow $query_dbuid $name
// db_getrow $query_dbuid "default" 
db_getrow = [
	db__name = (getvalue (getalias "arg2") "default")
	cases (db_get_engine $db__name) "sqlite3" [
		db__res = (sqlite3_getrow $arg1)
	] "mysql" [
		db__res = (mysql_getrow $arg1)
	] () [
		db__res = ""
	]
	result $db__res
]

// finalize result
// db_finalize $query_dbuid $name
// db_finalize $query_dbuid "default" 
db_finalize = [
	db__name = (getvalue (getalias "arg2") "default")
	cases (db_get_engine $db__name) "sqlite3" [
		db__res = (sqlite3_finalize $arg1)
	] "mysql" [		
		db__res = (mysql_finalize $arg1)
	] () [
		db__res = -1
	]
	result $db__res
]


// close database
// db_close $name $conn_num
// db_close "default"  0
db_close = [

	db__name = (getvalue (getalias "arg1") "default")
	db__conn_num = (getvalue (getalias "arg1") "0")

	db__uid = (db_get_dbuid $db__name $db__conn_num)
	
	cases (db_get_engine $arg1) "sqlite3" [
		db__res = (sqlite3_close $db__uid)
	] "mysql" [
		db__res = (mysql_close $db__uid)
	] () [ // default
		db__res = -1
	]
	result $db__res
]
