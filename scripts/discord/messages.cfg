//------ onconnect [cn] ------//
discord_onconnect = [ discordsay (format "CONNECT %1(%2) (%3)" (getname $arg1) $arg1 (getcountry (getip $arg1))) ]
addhandler onconnect discord_onconnect

//------ ondisconnect [cn] ------//
discord_ondisc = [ discordsay (format "DISCONNECT %1(%2) - Connection time: %3" (getname $arg1) $arg1 (formatmillis "%hh %mm %ss" (getonline $arg1))) ]
addhandler ondisconnect discord_ondisc

discord_mapstart = [ discordsay (format "MAP CHANGED: %1 %2 %3/%4 players" (modetostr (getmode)) (getmap) (numclients) $maxclients) ]
addhandler onmapstart discord_mapstart

discord_onsetmaster = [
	txt = (format "%1 %2(%3) %4 %5" "MASTER:" (getname $arg1) $arg1 (? $arg2 "claimed" "relinquished") (? $arg2 (privname $arg2) "priveleges"))
	if (!=s $arg4 "") [ txt = (format "%1 as '%2'" $txt $arg4) ]
	if (!=s $arg5 "") [ txt = (format "%1 [%2]" $txt $arg5) ]
	discordsay $txt
]
addhandler onsetmaster discord_onsetmaster

discord_onmastermode = [ discordsay (format "MM: %1(%2) set mastermode to %3" (getname $arg1) $arg1 $arg2) ]
addhandler onmastermode discord_onmastermode

discord_onswitchname = [ discordsay (format "%1(%2) is now known as %3" (ircorange $arg3) $arg1 (ircorange $arg2) ) ]
addhandler onswitchname discord_onswitchname

discord_onkick = [
	discordsay (format "KB: %1(%2) (%3) was kicked from server by %4(%5)" (getname $arg2) $arg2 (getip $arg2) (getname $arg1) (? (= $arg1 -1) "console" $arg1))
]
addhandler onkick discord_onkick


//------ @ontext [cn] [msg] ------//
discord_text_handler = [ discordsay (format "%1(%2): %3" (getname $arg1) $arg1 $arg2) ]
addhandler ontext discord_text_handler

discord_onmsg_handler = [
    say (format "^f2[discord]^f9%1: ^f0%2" $arg1 $arg4)
]

addhandler discord_onmsg discord_onmsg_handler

discord_showbest = [
	infoline = "GAME ENDED"
	curr_mode = (modetostr (getmode))   
	infoline = (concat $infoline $curr_mode)  
	infoline = (concat $infoline (getmap))
	infoline = (concat $infoline (format "players: %1/%2" (numclients) $maxclients)) 
	
	//if current gamemode is team mode
	if (isinlist $TEAMMODES (modetostr (getmode))) [	
		awdline = (concat $awdline (format "good: %1 evil: %2" (getteamscores "good") (getteamscores "evil"))) 
	]          
	
	// Best 3
	if (> (numclients) 0) [
	    all = (allplayers)
	    fpline = "|"
	    loop i	3 [
	    	n = (+ $i 1)
	    	looplist cn $all [     
	    		if (= (getrank $cn) $n) [
	    			fpline = (concat $fpline (format "%1. %2(%3) (%4/%5 %6%%)" $n (getname $cn) $cn (getfrags $cn) (getdeaths $cn) (getaccuracy $cn)))        		
	    		] 
	    	]               		        	        	        	
	    ]                          
	    infoline = (concat $infoline $fpline)  
	]
	discordsay $infoline   
]

addhandler onimission discord_showbest
