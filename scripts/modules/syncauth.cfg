// script excuted once at server start

defaultvalue "auth_db" "" // database for use if empty dont use databse
defaultvalue "auth_table" "auth"

//************************************************
if (!=s $auth_db "") [
	not_recommended_with_sqlite "auth" $auth_db
	
	// Check if DB table exists and has proper columns
	sqlite_create_table = "CREATE TABLE `:0` (id INTEGER PRIMARY KEY AUTOINCREMENT, name VARCHAR(30), pubkey VARCHAR(49), rights VARCHAR(1), enabled TINYINT, unique(id, name))"
	mysql_create_table = "CREATE TABLE IF NOT EXISTS `:0` (id int(10) unsigned NOT NULL AUTO_INCREMENT, name varchar(30) NOT NULL, pubkey varchar(49) NOT NULL, rights varchar(1) NOT NULL, enabled tinyint(4) NOT NULL DEFAULT '1', PRIMARY KEY (`id`), UNIQUE KEY `name` (`name`)) ENGINE=InnoDB DEFAULT CHARSET=utf8 COLLATE=utf8_general_ci"
	db_check_table "auth" $auth_db $auth_table "id name pubkey rights enabled" $sqlite_create_table $mysql_create_table
]


auth_load_from_db = [
	// reload authkeys from database
	log_info (format "Auth: loading authkeys from database '%1'" $auth_db)

	db_init $auth_db

	authkeys_query = (format "SELECT name, pubkey, rights FROM %1 WHERE enabled='1'" $auth_table)
	qh = (db_query $authkeys_query $auth_db)
	
	// check for errors
	if (= $qh -1) [
		db_error $auth_db
		res = 0
	] [
		// no errors
		authcount = 0
		while [row = (db_getrow $qh $auth_db); result (!=s $row "")] [
			// row format "degrave 23abc54bca4b3c5bc 0"
			adduser (listat $row 0) $serverauth (listat $row 1) (listat $row 2)
			authcount = (+ 1 $authcount)
		]
		db_finalize $qh $auth_db
		log_info (format "Loaded %1 authkeys from database" $authcount)
		res = $authcount
	]
	result $res
]

if (!=s $auth_db "") [
	auth_load_from_db
]

cmd_syncauth = [
	// reaload auth.cfg other authkeys will disappear
	reloadauth

	syncmsg = "Authkeys reloaded"

	// load keys from database if needed
	if (!=s $auth_db "") [
		authcount = (auth_load_from_db)
		syncmsg = (format "Authkeys reloaded, %1 authkeys were loaded from database" $authcount)
	]
	
	pm $arg1 $syncmsg
]



irccmd_syncauth = [
	// reaload auth.cfg other authkeys will disappear
	reloadauth

	syncmsg = "Authkeys reloaded"

	// load keys from database if needed
	if (!=s $auth_db "") [
		authcount = (auth_load_from_db)
		syncmsg = (format "Authkeys reloaded, %1 authkeys were loaded from database" $authcount)
	]
	
	ircsay (format "%1, %2" $arg1 $syncmsg)
]
